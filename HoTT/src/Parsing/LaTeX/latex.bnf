// ----------------------------------------------------------------------------- Base

@ <start> ::= <content>* "\\end{document}" /[\n]+/?
@ <content> ::= <rule> | <other_content>

// definition
@ <notation> ::= "\\notation{" <text> "}" "\\probe"
@ <actual_definition> ::= "\\actualDef{" <text> "}" "\\probe"
@ <def_id> ::= <text>
@ <definition> ::= "\\begin{definition}{" <def_id> "}" <other_content>* <notation> <other_content>? <actual_definition> <other_content>? "\\end{definition}"
    | "\\begin{definition}{" <def_id> "}" <other_content>* <actual_definition> <other_content>? <notation> <other_content>? "\\end{definition}"

// axiom
@ <axiom_name> ::= "\\axiomName{" <text> "}" "\\probe"
@ <actual_axiom> ::= "\\actualAxiom{" <any_text> "}" "\\probe"
@ <axiom_id> ::= <text>
@ <axiom> ::= "\\begin{axiom}{" <axiom_id> "}" <other_content>* <axiom_name> <other_content>? <actual_axiom> <other_content>? "\\end{axiom}"
    | "\\begin{axiom}{" <axiom_id> "}" <other_content>* <actual_axiom> <other_content>? <axiom_name> <other_content>? "\\end{axiom}"

// inference
@ <axiom_apply> ::= "\\axiomApply{" <any_text> "}" "\\probe" | "\\axiomApplyEmpty{" <any_text> "}" "\\probe"
@ <inference> ::= (<axiom_apply> "\n"?)+

// context environment
@ <ctxenv_name> ::= "\\ctxenvName{" <text> "}" "\\probe"
@ <actual_ctxenv> ::= "\\actualCtxenv{" <inference> "}" "\\probe"
@ <ctxenv_id> ::= <text>
@ <context_env> ::= "\\begin{context_env}{" <ctxenv_id> "}" <other_content>* <ctxenv_name> <other_content>? <actual_ctxenv> <other_content>? "\\end{context_env}"

// construction
@ <construction_name> ::= "\\constructionName{" <text> "}" "\\probe"
@ <actual_construction> ::= "\\actualConstruction{" <any_text> "}" "\\probe"
@ <construction_id> ::= <text>
@ <construction> ::= "\\begin{construction}{" <construction_id> "}" <other_content>* <construction_name> <other_content>? <actual_construction> <other_content>? "\\end{construction}"


@ <rule> ::= <definition> | <axiom> | <context_env> | <construction>

// ----------------------------------------------------------------------------- latex stuff (mainly to tell the parser to ignore those)

@ <arguments> ::= (<optional_args> | <balanced_braces>)+
@ <other_content> ::= <text> | <latex_command> | <balanced_braces> | <comment>
@ <balanced_braces> ::= "{" <balanced_content> "}"
@ <balanced_content> ::= ( <text> | <latex_command> | <balanced_braces> )*
@ <latex_command> ::= "\\" <command_name> (<balanced_braces> | <optional_args>)*
@ <command_name>  ::= /[a-zA-Z]+/
@ <optional_args> ::= "[" <text> "]"
@ <other_latex>   ::= <text> | <latex_command> | <balanced_braces> | <comment>
@ <text>          ::= /[^{}\[\]\\%]+/
@ <any_text>      ::= /(?:[^{}]|\{(?!\s*\\probe\})|\}(?!\s*\\probe))+/      -- any text that does not end with "}\probe" (taking whitespaces into account)
@ <comment>       ::= "%" /[^\n]+/ "\n"
